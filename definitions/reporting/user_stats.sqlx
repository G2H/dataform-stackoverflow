config {
  type: "view",
  schema: "reporting",
  tags: ["daily", "reporting"],
  description: "Create a summary table for all users including statistics on questions, answeers and badges received.",
  columns: {
    user_id: `A unique identifier for a user`,
    age: `The age of a user`,
    creation_date: `The date this user signed up`,
    user_tenure: `The number of years since the user's creation date`,
    badge_count: `The all-time number of badges the user has received`,
    questions_and_answer_count: `The all-time number of questions and answers the user has created`,
    question_count: `The all-time number of questions the user has created`,
    answer_count: `The all-time number of answers the user has created`,
    last_badge_received_at: `The time the user received their most recent badge`,
    last_posted_at: `The time the user last posted a question or answer`,
    last_question_posted_at: `The time the user last posted an answer`,
    last_answer_posted_at: `The time the user last posted a question`,
  },
  assertions: {
    uniqueKey: ["user_id"],
    rowConditions: ["badge_count >= 0"]
  }
}

select
  stg_users.user_id,
  stg_users.age,
  ${common.ageBucket("stg_users.age")} as age_bucket,
  stg_users.creation_date,
  stg_users.user_tenure,
  count(distinct stg_badges.badge_id) as badge_count,
  max(posts_all.created_at) as last_posted_at,
  max(if(type="question", posts_all.created_at, null)) as last_question_posted_at,
  max(if(type="answer", posts_all.created_at, null)) as last_answer_posted_at
from
  ${ref("stg_users")} as stg_users
  left join ${ref("stg_badges")} as stg_badges
    on stg_users.user_id = stg_badges.user_id
  left join ${ref("posts_combined")} as posts_all
    on stg_users.user_id = posts_all.owner_user_id
group by
  1,2,3,4,5


